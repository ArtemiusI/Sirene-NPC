COPY ~%MOD_FOLDER%/2da~ ~override~
COPY ~%MOD_FOLDER%/audio~ ~override~
COPY ~%MOD_FOLDER%/spells~ ~override~

ADD_KIT ~C0ILM~

 // appended to CLASWEAP.2da
 ~C0ILM		1           1           1           1           1           1           1           1~

 // appended column-wise to WEAPPROF.2da
//11                                T   S           F   Q             S S
//10                                W   C           L   U             W I
//9                         B L S   O   I   W       A   A             O N
//8                         A O H   H   M   A       I   R C   S       R G
//7         L S           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6         _ _       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5         S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4         W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3         O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2         R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1         D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
 ~C0ILM		2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 0 0 0 0 0 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
    
 // appended to ABCLASRQ.2da
 ~C0ILM				12      0	9	0	13	17~

 // appended to ABCLSMOD.2da
 ~C0ILM				0	0	0	0	0	0~

 // appended to ABDCDSRQ.2da
 ~C0ILM                		0       0       0       0       0       0~

 // appended to ABDCSCRQ.2da
 ~C0ILM                 	0       0       0       0       0	0~

 // appended to ALIGNMNT.2da
 ~C0ILM                  	1       0       0       0       0	0       0       0       0~

 // appended to DUALCLAS.2da
 ~C0ILM                  	0       0       0       0       0       0~

 // path to your CLAB-style 2da file 
 ~%MOD_FOLDER%/2da/C0ILMCL.2da~

 // PC races and classes allowed to use this kit, from KITTABLE.2da
 ~K_P_H K_P_E K_P_HE K_P_HL K_P_G K_P_D K_P_HO~

 // usability flags, added to the end of the KITLIST.2da entry
 ~0x00000008     6~

 // HLA table to use; see LUABBR.2da
 ~PA0~

 // list of starting equipment for PCs starting in ToB. Appended column-wise to 25STWEAP.2da
 ~CHAN09 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN4,2 POTN14,5 HAM07 SW1H27 STAF08~

 // Lower, mixed, and help kit names and descriptions, added to KITLIST.2da
  SAY ~martyr~
  SAY ~Martyr~
  SAY ~MARTYR: Martyrs are worshippers of Ilmater, the good god of suffering, endurance and perseverance. Paladins of Ilmater's order are compassionate and train themselves to become protectors of their allies and to alleviate the suffering of others.

Advantages:
– May use Lay on Hands three times per day.
– Gains 5% resistance to crushing, slashing, piercing, and missile damage every 5 levels to a maximum of 20% at level 20.
– May use the Sacrifice ability once per day. Gains an additional use at 5th level and every 5 levels thereafter.

SACRIFICE: The martyr transfers 10 hit points plus 2 per level to the target, up to a maximum of 50 hit points at 20th level. The martyr cannot be killed by the sacrifice—if their hit points would go below 1, they are knocked unconscious instead.

– May use the Martyrdom ability once per day starting at level 5. Gains an additional use at 10th level and every 10 levels thereafter.

MARTYRDOM: The martyr takes on a defensive stance to protect their comrades. For one turn, the martyr exudes an aura which grants all allies within 15-ft. a +3 bonus to Armor Class and 15% resistance to physical damage while they gain twice the amount. Allies who leave the range of the martyr's aura retain the benefits for 2 rounds. The martyr's damage and movement rate are halved while in this stance, but their effective personal space is greatly increased, preventing creatures from passing them in a 5-ft. radius. At the end of the duration, the martyr is greatly fatigued. This ability may not be used in conjuction with Hardiness.

– 9th level: Gains the Inspirational Martyr passive ability.

INSPIRATIONAL MARTYR: The martyr's dauntless heroism and endurance keeps their allies motivated and safe, even in death. When the martyr's maximum hit points falls below 50%, all allies (except the martyr) within sight range gain a +1 bonus to hit, damage and saving throws. If the martyr is slain, all allies are healed for 50% of their maximum hit points and gain +4 to hit, damage and saving throws for two turns.

– 12th level: The martyr becomes immune to fatigue.
– 15th level: May use the Ilmater's Grace ability once per day.

ILMATER'S GRACE: The martyr may call upon a second wind in battle, fully restoring and doubling their maximum hit points for one turn.

– Hit Die: d12

Disadvantages:
– Incurs a -1 penalty to damage.
– May not cast Protection from Evil.
– May not Turn Undead.
– May not use ranged weapons.~

LAF fl#add_kit_ee
  STR_VAR
    kit_name = C0ILM
    clswpbon = ~1 0 3~
	hpclass = ~HPBARB~
	clsrcreq = ~1 0 0 0 0 0 0~
    numwslot = 3
END

DEFINE_ACTION_FUNCTION cd_new_portrait_icon
  INT_VAR string = 0
  STR_VAR bam_file = "****"
  RET     icon
  BEGIN

  COPY_EXISTING ~statdesc.2da~ ~override~
    COUNT_2DA_ROWS 3 count
    READ_2DA_ENTRY (count - 1) 0 3 icon
    SET icon += 1

  APPEND ~statdesc.2da~ ~%icon% %string% %bam_file%~ UNLESS ~%bam_file%~

END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Martyrdom~) STR_VAR bam_file = c0ilms2i RET c0ilms2i = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Heroic Inspiration~) STR_VAR bam_file = c0ilms4i RET c0ilms4i = icon END

COPY ~%MOD_FOLDER%/spells/C0ilmi2.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 139
	  parameter1 = RESOLVE_STR_REF (~Gained Passive Ability: Inspirational Martyr~)
END

COPY ~%MOD_FOLDER%/spells/c0ilms1.spl~ ~override~
SAY NAME1 ~Sacrifice~
SAY NAME2 ~Sacrifice~
SAY UNIDENTIFIED_DESC ~Sacrifice
The martyr transfers 10 hit points plus 2 per level to the target, up to a maximum of 50 hit points at 20th level. The martyr cannot be killed by the sacrifice—if their hit points would go below 1, they are knocked unconscious instead.~

COPY ~%MOD_FOLDER%/spells/C0ilms2.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 142
	  parameter2 = c0ilms2i
END
SAY NAME1 ~Martyrdom~
SAY NAME2 ~Martyrdom~
SAY UNIDENTIFIED_DESC ~Martyrdom
The martyr takes on a defensive stance to protect their comrades. For one turn, the martyr exudes an aura which grants all allies within 15-ft. a +3 bonus to Armor Class and 15% resistance to physical damage while they gain twice the amount. Allies who leave the range of the martyr's aura retain the benefits for 2 rounds. The martyr's damage and movement rate are halved while in this stance, but their effective personal space is greatly increased, preventing creatures from passing them in a 5-ft. radius. At the end of the duration, the martyr is greatly fatigued. This ability may not be used in conjuction with Hardiness.~

COPY ~%MOD_FOLDER%/spells/C0ilms3.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 139
	  parameter1 = RESOLVE_STR_REF (~Maximum Hit Points Doubled~)
END
SAY NAME1 ~Ilmater's Grace~
SAY NAME2 ~Ilmater's Grace~
SAY UNIDENTIFIED_DESC ~Ilmater's Grace
The martyr may call upon a second wind in battle, fully restoring and doubling their maximum hit points for one turn.~

COPY ~%MOD_FOLDER%/spells/C0ilms4.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 139
	  parameter1 = RESOLVE_STR_REF (~Heroic Inspiration~)
END
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 142
	  parameter2 = c0ilms4i
END

COPY ~%MOD_FOLDER%/spells/C0ilms5.spl~ ~override~
SAY NAME1 ~Inspirational Martyr~

EXTEND_TOP ~AR3400.bcs~ ~%MOD_FOLDER%/Scripts/AR3400.baf~

APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

COPY ~%MOD_FOLDER%/creatures/C0Sirene.cre~ ~override/C0Sirene.cre~
  SAY NAME1 ~Sirene~
  SAY NAME2 ~Sirene~
  WRITE_ASCII 0x248 ~C0Sirene~ #8  //override script
  WRITE_ASCII 0x280 ~C0Sirene~ #32  //DV
  WRITE_ASCII 0x2cc ~C0Sirene~ #8   //pre-joining dialogue file
  WRITE_ASCII 0X34 ~SireneM~ #8   //small portrait
  WRITE_ASCII 0x3c ~SireneL~ #8   //medium portrait
  SAY BIO ~When asked about her past, SIRENE averts her gaze and flatly states that there is little to say. She was orphaned and left to die as a babe, likely abandoned due to her apparent fiendish heritage. Only by luck did she survive certain death by being discovered by a travelling priest of Ilmater, who brought her to others of his faith and raised her as a Holy Warrior of Suffering, the order of paladins dedicated to the Crying God. She resents her own contradictory nature, though she is unsure of which side.~
  SAY MORALE ~I'll not die this day!~ [C0SIR001]
  SAY HAPPY ~You are... an inspiration to me.~
  SAY UNHAPPY_ANNOYED ~I do not approve of the path you're taking us.~
  SAY UNHAPPY_SERIOUS ~My mercy and patience are running thin.~
  SAY UNHAPPY_BREAKING_POINT ~I'll not be a martyr to your evil cause.~
  SAY LEADER ~I shall lead by example.~ [C0SIR002]
  SAY TIRED ~I am exhausted.~ [C0SIR003]
  SAY BORED ~Are we waiting for something?~ [C0SIR004]
  SAY BATTLE_CRY1 ~Attack!~ [C0SIR005]
  SAY BATTLE_CRY2 ~Make peace with your gods!~ [C0SIR006]
  SAY BATTLE_CRY3 ~To battle!~ [C0SIR007]
  SAY BATTLE_CRY4 ~In Ilmater's name!~
  SAY BATTLE_CRY5 ~For the Crying God!~
  SAY DAMAGE ~Ah!~ [C0SIR008]
  SAY DYING ~No... uh...~ [C0SIR009]
  SAY HURT ~Ugh, my wounds need tending!~ [C0SIR010]
  SAY AREA_FOREST ~The forest is truly beautiful, and I appreciate the calm.~
  SAY AREA_CITY ~The city is a haven of suffering...~
  SAY AREA_DUNGEON ~There is evil amongst the darkness, my friend. Stay alert.~
  SAY AREA_DAY ~'Tis a beautiful day.~
  SAY AREA_NIGHT ~The dark will not hide our enemies.~
  SAY SELECT_COMMON1 ~You have need of me?~ [C0SIR011]
  SAY SELECT_COMMON2 ~I am by your side.~ [C0SIR012]
  SAY SELECT_COMMON3 ~Yes?~ [C0SIR013]
  SAY SELECT_COMMON4 ~You have need of me?~ [C0SIR011]
  SAY SELECT_COMMON5 ~I am by your side.~ [C0SIR012]
  SAY SELECT_COMMON6 ~Yes?~ [C0SIR013]
  SAY SELECT_ACTION1 ~Certainly.~ [C0SIR014]
  SAY SELECT_ACTION2 ~Very well.~ [C0SIR015]
  SAY SELECT_ACTION3 ~As you wish.~ [C0SIR016]
  SAY SELECT_ACTION4 ~How can I help?~ [C0SIR017]
  SAY SELECT_ACTION5 ~Ready and willing.~ [C0SIR018]
  SAY SELECT_ACTION6 ~Tell me your needs.~ [C0SIR019]
  SAY SELECT_ACTION7 ~I am here.~ [C0SIR020]
  SAY CRITICAL_HIT ~Your judgment is at hand!~[C0SIR021]
  SAY CRITICAL_MISS ~Ugh!~ [C0SIR022]
  SAY TARGET_IMMUNE ~What? That can't be!~
  SAY INVENTORY_FULL ~I dropped that, 'twas too much of a burden.~
  SAY SPELL_DISRUPTED ~Ilmater forgive me...~
  SAY SET_A_TRAP ~~ []
  SAY HIDDEN_IN_SHADOWS ~~ []
  SAY PICKED_POCKET ~~ []
  WRITE_SHORT 0x246 (IDS_OF_SYMBOL (~kit~ ~C0ILM~))

COMPILE	~%MOD_FOLDER%/Dialogue/BC0Siren.d~ 
	~%MOD_FOLDER%/Dialogue/C0Sirene.d~ 
	~%MOD_FOLDER%/Dialogue/C0SirenEE.d~ 
	~%MOD_FOLDER%/Dialogue/C0SirenJ.d~
	~%MOD_FOLDER%/Dialogue/C0SirenP.d~

	~%MOD_FOLDER%/Scripts/C0DornF.baf~ 
	~%MOD_FOLDER%/Scripts/C0Sirene.baf~
	~%MOD_FOLDER%/Scripts/C0SirenD.baf~
	~%MOD_FOLDER%/Scripts/C0reset.baf~

EXTEND_BOTTOM ~Dorn.bcs~ ~%MOD_FOLDER%/scripts/c0dornf.baf~

APPEND ~pdialog.2da~
~C0Sirene        C0SirenP	C0SirenJ	C0SirenD~
UNLESS ~C0Sirene~

APPEND ~interdia.2da~
~C0Sirene       BC0Siren C0SirenEE~
UNLESS ~C0Sirene~
  
COPY ~%MOD_FOLDER%/items/C0sw2h.BAM~ ~override~
COPY ~%MOD_FOLDER%/items/C0csw2h.BAM~ ~override~
COPY ~%MOD_FOLDER%/Items/C0SW2H.itm~ ~override/C0SW2H.itm~
SAY NAME1 ~Two-handed Sword~
SAY NAME2 ~Brimstone +1~
SAY DESC ~This two-handed sword is warm as though left under the sun and smells faintly of smoke. Not only is it a well balanced weapon, but each swing in battle releases a trail of burning ash which disorients foes and makes it more difficult to retaliate.

This weapon can only be wielded by tieflings.

STATISTICS:

Combat abilities:
– Each hit decreases the opponent's THAC0 by 1 for 2 rounds (save vs. breath to avoid)

Equipped abilities:
– -1 Constitution
– 20% resistance to fire damage

THAC0: +1
Damage: 1d10+1, 1 fire damage (slashing)
Speed Factor: 8
Proficiency Type: Two-handed Sword
Type: Two-handed
Requires:
 14 Strength

Weight: 10~

// Sirene-White
ACTION_IF FILE_EXISTS_IN_GAME ~bc0siren.dlg~ AND FILE_EXISTS_IN_GAME ~V#1WHIB.dlg~
THEN BEGIN
  PRINT ~Adding crossmod banters between Sirene and White~
COMPILE ~%MOD_FOLDER%/dialogue/crossmod/white.d~
END

// Sirene-Verr'Sza
ACTION_IF FILE_EXISTS_IN_GAME ~bc0siren.dlg~ AND FILE_EXISTS_IN_GAME ~L#1VerrB.dlg~
THEN BEGIN
  PRINT ~Adding crossmod banters between Sirene and Verr'Sza~
COMPILE ~%MOD_FOLDER%/dialogue/crossmod/verrsza.d~
END

// Sirene-Drake
ACTION_IF FILE_EXISTS_IN_GAME ~bc0siren.dlg~ AND FILE_EXISTS_IN_GAME ~bc0drake.dlg~
THEN BEGIN
  PRINT ~Adding crossmod banters between Sirene and Drake~
COMPILE ~%MOD_FOLDER%/dialogue/crossmod/drake.d~
END

// Sirene-Aura
ACTION_IF FILE_EXISTS_IN_GAME ~bc0siren.dlg~ AND FILE_EXISTS_IN_GAME ~bc0aura.dlg~
THEN BEGIN
  PRINT ~Adding crossmod banters between Sirene and Aura~
COMPILE ~%MOD_FOLDER%/dialogue/crossmod/aura.d~
END

PRINT ~Choose a portrait for Sirene (check mod folder for portraits):~
PRINT ~Please choose one of the following:
[1] BG1 Default by sporeboy
[2] BG2 Default by Isandir
[3] Alternate Light Armor by Sirick
[4] Alternate Heavy Armor by Sirick/LavaDelVortel
[5] Alternate Portrait by Kyllian Guillart (https://www.deviantart.com/lodaligae)~

ACTION_READLN ~portrait~
OUTER_WHILE (!(IS_AN_INT ~portrait~) OR (~%portrait%~ > 5) OR (~%portrait%~ < 1)) BEGIN
  PRINT ~Please choose one of the following:
[1] BG1 Default by sporeboy
[2] BG2 Default by Isandir
[3] Alternate Light Armor by Sirick
[4] Alternate Heavy Armor by Sirick/LavaDelVortel
[5] Alternate Portrait by Kyllian Guillart (https://www.deviantart.com/lodaligae)~ // repeat prompt until valid answer
  ACTION_READLN ~portrait~
END

ACTION_IF (~%portrait%~ = 1) THEN BEGIN
  PRINT ~Installing Portrait: BG1 Default by sporeboy~
		COPY ~%MOD_FOLDER%/portraits/Sirene.bmp~ ~override/SireneL.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene.bmp~ ~override/SireneM.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene.bmp~ ~override/SireneS.bmp~
END ELSE ACTION_IF (~%portrait%~ = 2) THEN BEGIN
  PRINT ~Installing Portrait: BG2 Default by Isandir~
		COPY ~%MOD_FOLDER%/portraits/Sirene2.bmp~ ~override/SireneL.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene2.bmp~ ~override/SireneM.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene2.bmp~ ~override/SireneS.bmp~
END ELSE ACTION_IF (~%portrait%~ = 3) THEN BEGIN
  PRINT ~Installing Portrait: Alternate Light Armor by Sirick~
		COPY ~%MOD_FOLDER%/portraits/Sirene3.bmp~ ~override/SireneL.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene3.bmp~ ~override/SireneM.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene3.bmp~ ~override/SireneS.bmp~
END ELSE ACTION_IF (~%portrait%~ = 4) THEN BEGIN
  PRINT ~Installing Portrait: Alternate Heavy Armor by Sirick/LavaDelVortel~
		COPY ~%MOD_FOLDER%/portraits/Sirene4.bmp~ ~override/SireneL.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene4.bmp~ ~override/SireneM.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene4.bmp~ ~override/SireneS.bmp~
END ELSE ACTION_IF (~%portrait%~ = 5) THEN BEGIN
  PRINT ~Installing Portrait: Alternate Heavy Armor by Sirick/LavaDelVortel~
		COPY ~%MOD_FOLDER%/portraits/Sirene5.bmp~ ~override/SireneL.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene5.bmp~ ~override/SireneM.bmp~
		COPY ~%MOD_FOLDER%/portraits/Sirene5.bmp~ ~override/SireneS.bmp~
END

////////////////////////////////////// Siege of Dragonspear
ACTION_IF GAME_INCLUDES ~sod~
THEN BEGIN
  PRINT ~Installing content for Siege of Dragonspear...~
INCLUDE ~%MOD_FOLDER%/lib/Sirene_sod.tpa~
END